from util.get_model import get_model
from util.snake.configs import get_mlperf_tiny_config

config = get_mlperf_tiny_config()


module default_rules:
    snakefile:
        "../../util/snake/default_rules.smk"
    config:
        config


module tf_rules:
    snakefile:
        "../../util/snake/tensorflow.smk"
    config:
        config


use rule * from default_rules exclude compile_llvm_module, snax_opt_mlir as default_*


use rule * from tf_rules as tf_*


nets = ["pretrainedResnet_quant", "kws_ref_model", "vww_96_int8", "ad01_int8"]
extensions = [".o"]


rule all:
    input:
        expand("{network}{extension}", network=nets, extension=extensions),


rule get_mlperf_tiny_model:
    output:
        "{network}.tflite",
    # We need to constrain this wildcard to inform snakemake of the limited
    # possibilties this function can make
    wildcard_constraints:
        network="|".join(nets),
    run:
        get_model(wildcards.network, output[0])


rule snax_opt_mlir:
    """
    Similar to snax_opt_mlir, but with print-op-generic
    """
    input:
        "{file}.mlir",
    output:
        temp("{file}.snax-opt.mlir"),
    shell:
        "{config[snax-opt]} -p {config[snaxoptflags]} --allow-unregistered-dialect --print-op-generic -o {output} {input}"


# Override default rule, for output not to be temporary
rule compile_llvm_module:
    input:
        "{file}.ll12",
    output:
        "{file}.o",
    shell:
        "{config[cc]} {config[clangflags]} -x ir -c {input} -o {output}"
