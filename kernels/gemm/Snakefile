from util.snake.configs import get_snax_gemmx_config

config = get_snax_gemmx_config()


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules exclude snax_opt_mlir as snax_*


rule generate_gemm:
    output:
        "gemm.mlir",
    script:
        "gemm.py"


rule generate_gemm_broadcast:
    output:
        "gemm_broadcast.mlir",
    script:
        "gemm_broadcast.py"


rule compile_snax:
    """
    Apply various transformations snax-opt on mlir files.
    Options controlled with `snaxoptflags` defined in config.
    """
    input:
        "{file}.mlir",
    output:
        temp("{file}.ll.mlir"),
    shell:
        "{config[snaxc]} -c {config[snaxc-config]} -o {output} {input}"


# Rules
rule all:
    input:
        "gemm_traces.json",
        "gemm_broadcast_traces.json",


rule compile_main:
    input:
        "main.c",
    output:
        "main.o",
    shell:
        "{config[cc]} {config[cflags]} -c {input[0]}"


rule link_snax_binary:
    input:
        "{file}.o",
        "main.o",
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"
