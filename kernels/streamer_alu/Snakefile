from util.snake.configs import get_snax_alu_config

config = get_snax_alu_config()
config["snaxoptflags"] = ",".join(
    [
        "preprocess",
        "insert-accfg-op{accelerator=snax_alu}",
        "convert-linalg-to-kernel",
        "dispatch-kernels",
        "test-insert-debugs",
        "set-memory-space",
        "set-memory-layout",
        "test-insert-debugs",
        "realize-memref-casts",
        "reuse-memref-allocs",
        "insert-sync-barrier",
        "dispatch-regions",
        "convert-linalg-to-dart",
        "dart-scheduler",
        "dart-layout-resolution",
        "convert-dart-to-snax-stream",
        "convert-linalg-to-accfg",
        "convert-accfg-to-csr",
        "snax-copy-to-dma",
        "memref-to-snax",
        "snax-to-func",
        "test-debug-to-func",
        "clear-memory-space",
        "postprocess",
    ]
)


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules as snax_*


files = ["streamer_add", "streamer_add_stream"]


# Rules
rule all:
    input:
        expand("{file}_traces.json", file=files),


from gendata import create_data_files


rule generate_data:
    output:
        "data.c",
        "data.h",
    run:
        create_data_files()


rule compile_simple_main:
    input:
        "main.c",
        "data.o",
    output:
        temp("main.o"),
    shell:
        "{config[cc]} {config[cflags]} -c {input} -o {output}"


rule link_snax_binary:
    input:
        "{file}.o",
        "data.o",
        "main.o",
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"
