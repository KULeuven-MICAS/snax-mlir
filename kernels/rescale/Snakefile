from util.snake.configs import get_snax_gemmx_config

config = get_snax_gemmx_config()
config["snaxoptflags"] = ",".join(
    [
        "insert-accfg-op{accelerator=snax_gemmx}",
        "dispatch-kernels",
        "set-memory-space",
        "convert-linalg-to-dart",
        "dart-scheduler",
        "set-memory-layout",
        "realize-memref-casts",
        "dart-layout-resolution",
        "insert-sync-barrier",
        "dispatch-regions{nb_cores=2}",
        "convert-dart-to-snax-stream",
        "convert-linalg-to-accfg",
        "convert-accfg-to-csr",
        "snax-copy-to-dma",
        "memref-to-snax",
        "snax-to-func",
        "clear-memory-space",
        "postprocess",
    ]
)


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules as snax_*


rule link_snax_binary:
    input:
        "rescale.o",
        "main.o",
        "data.o",
    output:
        "rescale.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"


rule all:
    input:
        "rescale_traces.json",


from gendata import create_data_files


rule generate_simple_data:
    output:
        "data.c",
        "data.h",
    run:
        create_data_files()


rule compile_main:
    input:
        "main.c",
        "data.h",
    output:
        temp("main.o"),
    shell:
        "{config[cc]} {config[cflags]} -c {input[0]} -o {output}"
