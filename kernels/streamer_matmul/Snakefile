from util.snake.configs import get_snax_gemmx_config

config = get_snax_gemmx_config()
config["snaxoptflags"] = ",".join(
    [
        "preprocess",
        "convert-linalg-to-kernel",
        "insert-accfg-op{accelerator=snax_gemmx}",
        "dispatch-kernels",
        "convert-linalg-to-dart",
        "dart-fuse-operations",
        "snax-bufferize",
        "alloc-to-global",
        "set-memory-space",
        "dart-scheduler",
        "set-memory-layout",
        "realize-memref-casts",
        "insert-sync-barrier",
        "dispatch-regions{nb_cores=2}",
        "dart-layout-resolution",
        "convert-dart-to-snax-stream",
        "convert-linalg-to-accfg",
        "convert-accfg-to-csr",
        "snax-copy-to-dma",
        "memref-to-snax",
        "snax-to-func",
        "snax-allocate",
        "clear-memory-space",
        "postprocess",
    ]
)


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules as snax_*


files = ["quantized_matmul", "tiled_quantized_matmul", "matmul_i8_out"]


# Rules
rule all:
    input:
        expand("{file}_traces.json", file=files),


rule generate_quantized_matmul:
    output:
        "quantized_matmul.mlir",
    script:
        "quantized_matmul.py"


rule generate_matmul_i8_out:
    output:
        "matmul_i8_out.mlir",
    script:
        "matmul_i8_out.py"


rule generate_tiled_quantized_matmul:
    output:
        "tiled_quantized_matmul.transform.mlir",
    script:
        "tiled_quantized_matmul.py"


rule apply_transforms_mlir:
    input:
        "{file}.transform.mlir",
    output:
        "{file}.mlir",
    shell:
        "{config[snax-opt]} -p frontend-transform -o {output} {input}"


def get_main_obj(wildcards):
    if "i8" in wildcards.file:
        return "main_i8.o"
    else:
        return "main.o"


rule link_snax_binary:
    input:
        "{file}.o",
        main=lambda wildcards: get_main_obj(wildcards),
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input[0]} {input.main} -o {output}"
