from util.snake.configs import get_snax_xdma_config

config = get_snax_xdma_config()


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules exclude snax_opt_mlir as snax_*


rule compile_snax:
    """
    Apply various transformations snax-opt on mlir files.
    Options controlled with `snaxoptflags` defined in config.
    """
    input:
        "{file}.mlir",
    output:
        temp("{file}.ll.mlir"),
    shell:
        "{config[snaxc]} -c {config[snaxc-config]} -o {output} {input}"


files = ["add"]


# Rules
rule all:
    input:
        expand("{file}_traces.json", file=files),


rule generate_add:
    output:
        "add.mlir",
    script:
        "add.py"


rule link_snax_binary:
    input:
        "{file}.o",
        "main.o",
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"
