from util.snake.configs import get_snax_mac_config

config = get_snax_mac_config()
config["snaxoptflags"] = ",".join(
    [
        "dispatch-kernels",
        "set-memory-space",
        "set-memory-layout",
        "realize-memref-casts",
        "reuse-memref-allocs",
        "insert-sync-barrier",
        "dispatch-regions",
        "snax-copy-to-dma",
        "memref-to-snax",
        "snax-to-func",
        "clear-memory-space",
        "postprocess",
    ]
)


module default_rules:
    snakefile:
        "../../util/snake/default_rules.smk"
    config:
        config


use rule * from default_rules as default_*


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules as snax_*


files = ["simple_copy"]


# Rules
rule all:
    input:
        expand(
            "{file}_trace_chip_{num_chips:02d}_hart_{num_harts:05d}.dasm",
            file=files,
            num_chips=range(config["num_chips"]),
            num_harts=range(config["num_harts"]),
        ),


rule compile_main:
    input:
        "main.c",
        "data.h",
    output:
        "main.o",
    shell:
        "{config[cc]} {config[cflags]} -c {input[0]}"


rule link_snax_binary:
    input:
        "simple_copy.o",
        "main.o",
        "data.o",
    output:
        "simple_copy.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"


from gendata import create_data_files


rule generate_data:
    output:
        "data.c",
        "data.h",
    run:
        create_data_files()
