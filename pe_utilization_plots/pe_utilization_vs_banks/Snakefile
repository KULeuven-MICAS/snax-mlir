from util.snake.configs import get_snax_gemmx_config

config_matmul = get_snax_gemmx_config()

config = get_snax_gemmx_config()


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules exclude snax_opt_mlir, simulate as snax_*


tests = ["matmul"]
systems = ["mat"]
archs = ["reg", "cached"]
# banks = ['1', '2', '4', '8', '16', '32', '64', '128']
banks = ['8','16', '32', '64', '128']


# Rules
rule all:
    input:
        "plot.pdf",


rule generate_plot:
    input:
        expand("{file}_summary.txt", file=tests),
    output:
        "plot.pdf",
    shell:
        "python plot.py"


rule extract_cycles:
    input:
        expand("{file}_sys{sys}_arch{arch}_bank{bank}_traces.json", file="{file}", sys=systems, bank=banks, arch=archs),
    output:
        "{file}_summary.txt",
    shell:
        "python extract_cycles.py {output} {input}"


rule simulate:
    input:
        "{file}_sys{sys}.x",
    output:
#        temp(
            expand(
                "{file}_sys{sys}_arch{arch}_bank{bank}_trace_chip_{num_chips:02d}_hart_{num_harts:05d}.dasm",
                file=["{file}"],
                sys=["{sys}"],
                arch=["{arch}"],
                bank=["{bank}"],
                num_chips=range(config["num_chips"]),
                num_harts=range(config["num_harts"]),
            ),
#        ),
    log:
        "{file}_sys{sys}_arch{arch}_bank{bank}.vltlog",
    params:
        vltsim=config_matmul["vltsim"],
    shell:
        "vlt_interconnect_{wildcards.arch}_2_fifo/snitch_cluster_{wildcards.bank}_banks.vlt --prefix-trace={wildcards.file}_sys{wildcards.sys}_arch{wildcards.arch}_bank{wildcards.bank}_ {input}  2>&1 | tee {log}"


# matmul of 560x360x192
rule generate_matmul:
    output:
        "matmul_sys{sys}.mlir",
    wildcard_constraints:
        sys="|".join(systems),
    shell:
        "python matmul.py --m 64 --n 64 --k 64 --output {output}"
        #"python matmul.py --m 40 --n 56 --k 72 --output {output}"
        # "python matmul.py --m 16 --n 8 --k 16 --output {output}"


rule compile_snax:
    input:
        "{file}_sys{sys}.mlir",
    output:
        temp("{file}_sys{sys}.ll.mlir"),
    params:
        snaxconfig=config_matmul["snaxc-config"],
    shell:
        "{config[snaxc]} -c {params.snaxconfig} --add-mcycle  -o {output} {input}"


def get_linked_files(wildcards):
    return [f"{wildcards.file}.o", "main_2d_i8.o"]


rule link_snax_binary:
    input:
        get_linked_files,
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"
