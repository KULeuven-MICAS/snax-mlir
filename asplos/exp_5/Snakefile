from util.snake.configs import get_asplos_resnet_config

import os

config = get_asplos_resnet_config()


configfile: "tiled_resnet_layers.yaml"


convspec_ids = [layer["layer"]["name"] for layer in config["layers"]]


rule plot_figure:
    input:
        "summary.txt",
    output:
        "plot.svg",
    script:
        "plot.py"


module snax_rules:
    snakefile:
        "../../util/snake/snax.smk"
    config:
        config


use rule * from snax_rules exclude snax_opt_mlir as snax_*


rule all:
    input:
        "summary.txt",


rule extract_cycles:
    input:
        expand("{file}_traces.json", file=convspec_ids),
    output:
        "summary.txt",
    shell:
        "python extract_cycles.py {output} {input}"


rule generate_convs:
    output:
        expand("{file}.mlir", file=convspec_ids),
    shell:
        "python generate_tiled_convs.py"


rule compile_snax:
    input:
        "{file}.mlir",
    output:
        temp("{file}.ll.mlir"),
    shell:
        "{config[snaxc]} -c {config[snaxc-config]} --layout default -o {output} {input}"


def get_linked_files(wildcards):
    if "conv" in wildcards.file:
        return [f"{wildcards.file}.o", "main_4d_i8.o"]
    else:
        return [f"{wildcards.file}.o", "main_2d_i8.o"]


rule link_snax_binary:
    input:
        get_linked_files,
    output:
        "{file}.x",
    shell:
        "{config[ld]} {config[ldflags]} {input} -o {output}"
